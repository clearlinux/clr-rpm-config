#!/usr/bin/gawk -f

{
  # Preprocess filename:
  # * Deduplicate forward slashes
  # * Strip the Maven repository path prefix
  # * Create an array of path components
  gsub(/\/\/+/, "/")
  sub("^.*/usr/share/java/\\.m2/repository/", "")
  split($0, parts, /\//)

  # Extract the rightmost components: file name, version, and artifact ID
  file = parts[length(parts)]
  ver = parts[length(parts) - 1]
  artifact_idx = length(parts) - 2
  artifact = parts[artifact_idx]

  # Construct group ID
  i = 1
  group = ""
  for (; i < artifact_idx - 1; i++) {
    group = sprintf("%s%s.", group, parts[i])
  }
  group = sprintf("%s%s", group, parts[i])

  # Detect artifact type
  type = ""
  if (file ~ /\.jar$/) {
    type = "jar"
  } else if (file ~ /\.pom$/) {
    type = "pom"
  } else if (file ~ /\.signature$/) {
    type = "signature"
  }

  # Detect classifier, if present
  classifier = ""
  regexp = sprintf("^%s-%s(-.+)?\\.%s$", artifact, ver, type)
  ret = match(file, regexp, capture)
  if (ret) {
    len = capture[1, "length"]
    if (len != 0) {
      # skip the separating hyphen
      start = capture[1, "start"] + 1
      len = len - 1
      classifier = substr(file, start, len)
    }
  }

  # Since classifiers are optional, make sure to add the
  # separating ':' if one is defined.
  if (classifier) {
    classifier = ":" classifier
  }

  # Replace hyphens with dots in the version; RPM allows a single hyphen in a
  # provider string, but only to indicate "VERSION-RELEASE".
  gsub(/-/, ".", ver)

  printf("mvn(%s:%s:%s%s) = %s\n", group, artifact, type, classifier, ver)
}


# vi: ft=awk sw=2 sts=2 et
